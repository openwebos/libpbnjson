// @@@LICENSE
//
//      Copyright (c) 2009-2013 LG Electronics, Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//
// LICENSE@@@

%define hash-function-name json_schema_keyword_hash
%define lookup-function-name json_schema_keyword_lookup
%readonly-tables
%pic
%compare-strncmp
%enum
%includes
%struct-type

%{
	#include "json_schema_grammar.h"

	// Suppress warnings in the gperf generated code
	#pragma GCC system_header
%}

struct JsonSchemaKeyword { char const *name; int token; };

%%
$ref,                 TOKEN_KEY_DREF
$schema,              TOKEN_KEY_DSCHEMA
additionalItems,      TOKEN_KEY_ADDITIONAL_ITEMS
additionalProperties, TOKEN_KEY_ADDITIONAL_PROPERTIES
allOf,                TOKEN_KEY_ALL_OF
anyOf,                TOKEN_KEY_ANY_OF
default,              TOKEN_KEY_DEFAULT
definitions,          TOKEN_KEY_DEFINITIONS
description,          TOKEN_KEY_DESCRIPTION
enum,                 TOKEN_KEY_ENUM
exclusiveMaximum,     TOKEN_KEY_EXCLUSIVE_MAXIMUM
exclusiveMinimum,     TOKEN_KEY_EXCLUSIVE_MINIMUM
extends,              TOKEN_KEY_EXTENDS
id,                   TOKEN_KEY_ID
items,                TOKEN_KEY_ITEMS
maxItems,             TOKEN_KEY_MAX_ITEMS
maxLength,            TOKEN_KEY_MAX_LENGTH
maxProperties,        TOKEN_KEY_MAX_PROPERTIES
maximum,              TOKEN_KEY_MAXIMUM
minItems,             TOKEN_KEY_MIN_ITEMS
minLength,            TOKEN_KEY_MIN_LENGTH
minProperties,        TOKEN_KEY_MIN_PROPERTIES
minimum,              TOKEN_KEY_MINIMUM
name,                 TOKEN_KEY_NAME
oneOf,                TOKEN_KEY_ONE_OF
properties,           TOKEN_KEY_PROPERTIES
required,             TOKEN_KEY_REQUIRED
title,                TOKEN_KEY_TITLE
type,                 TOKEN_KEY_TYPE
